# Maintainer: Otreblan <otreblain@gmail.com>
# shellcheck shell=bash disable=SC2034,SC2164,SC2154,SC2155

pkgname=family-scraper-git
pkgver=r2.0bc5cc2
pkgrel=1
pkgdesc="Family scraper"
arch=('any')
url="https://github.com/otreblan/family-scraper"
license=('GPL')
depends=(
	"bash-completion"
	"python-selenium"
)
makedepends=(
	"python-build"
	"python-flit"
	"python-installer"
	"python-wheel"
)
checkdepends=()
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
_gitrepo=$(git rev-parse --show-toplevel)
_headcommit=$(git rev-parse HEAD)
source=("$pkgname::git+file://$_gitrepo#commit=$_headcommit")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cd "$srcdir/$pkgname"

	python -m build --wheel --no-isolation
}

package() {
	cd "$srcdir/$pkgname"

	python -m installer --destdir="$pkgdir" dist/*.whl

	local _completiondir="$pkgdir/$(pkg-config bash-completion --variable=completionsdir)"
	mkdir -p "$_completiondir"
	echo "complete -F _longopt ${pkgname%-git}" > "$_completiondir/${pkgname%-git}"
}
